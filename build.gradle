plugins {
    // Source code
    id 'java'

    // IDE
    id 'eclipse'
    id 'idea'

    // Documentation
    id 'org.asciidoctor.jvm.convert' version '3.0.0-alpha.2'
    id 'org.asciidoctor.jvm.epub'    version '3.0.0-alpha.2'
    id 'org.asciidoctor.jvm.gems'    version '3.0.0-alpha.2'
    id 'org.asciidoctor.jvm.pdf'     version '3.0.0-alpha.2'
}

repositories {
    jcenter()
    maven { url 'http://rubygems-proxy.torquebox.org/releases' }
}

dependencies {
    asciidoctorGems 'rubygems:asciimath:1.0.8'
}

wrapper { gradleVersion = '5.4.1' }

asciidoctorj {
    modules {
        diagram.version '1.5.18'
        epub.version '1.5.0-alpha.9'
        pdf.version '1.5.0-alpha.17'
    }

    requires 'asciimath'

    attributes = [
        sourcedir: project.sourceSets.main.java.srcDirs[0],
        'source-highlighter': 'coderay'
    ]
}

asciidoctor {
    dependsOn asciidoctorGemsPrepare
}

asciidoctorEpub {
    dependsOn asciidoctorGemsPrepare
    ebookFormats EPUB3

    attributes = [
        'data-uri': file("$buildDir/tmp/asciidoctorEpub/images"),
        imagesoutdir: file("$buildDir/tmp/asciidoctorEpub/images")
    ]
}

asciidoctorPdf {
    dependsOn asciidoctorGemsPrepare
}

kindlegen {
    agreeToTermsOfUse = true
}
